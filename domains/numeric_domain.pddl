(define (domain ai4ro2_E)
    (:requirements :adl :typing :fluents)
    (:types 
			place drink waiter - object
			Bar - place
	)
    (:predicates 
		(at-waiter ?w - waiter ?t  - place)
        (tray-taken)
		(tray-empty)
        (tray-carried ?w - waiter)
        (hand-free ?w - waiter)
        (clean ?t - place)
        (ordered ?d - drink ?t - place)
		(has3 ?w - waiter)(has2 ?w - waiter)(has1 ?w - waiter)
		
		(order-ready ?d - drink)
		(order-delivered ?d - drink)
		(order-carried ?d - drink ?w - waiter)
		(order-prepared ?d - drink)
		
		;(is-Bar ?t - place)
		(empty ?t - place)
		
		(equals ?d1 - drink ?d2 - drink)
	)
	(:functions 
		(distance ?t1 - place ?t2 - place)
		(fl-table-size ?t - place)
		(fl-hot ?d - drink)
		(time-barista)
		(time-waiter ?w - waiter)
		(time-drink-ready ?d - drink)
		(fl-customers ?t - place)
	    (fl-last-delivered ?t - place)
		(fl-time-empty ?t - place)
	)
	
    ;(:action wait-waiter    
    ;    :parameters  (?w - waiter)
    ;    ;:precondition 	()
    ;    :effect (increase (time-waiter ?w) 0.5)
	;)
	;(:action wait-barista    
    ;    ;:parameters  ()
    ;    ;:precondition 	()
    ;    :effect (increase (time-barista) 1)
	;)
	(:action prepare
		:parameters (?d - drink)
		:precondition (not (order-prepared ?d))
        :effect (and
					(increase (time-barista) (+  3   (* 2 (fl-hot ?d ))  ) )
					(assign (time-drink-ready ?d) (+ (+  3   (* 2 (fl-hot ?d ))  )(time-barista)))
					(order-ready ?d)
					(order-prepared ?d)
				)
	)       

	(:action move-tray  
        :parameters  (?from - place ?to - place ?w - waiter)
        :precondition (and(at-waiter ?w ?from)(tray-carried ?w))
        :effect (and  (not(at-waiter ?w ?from)) (at-waiter ?w ?to)
						(increase (time-waiter ?w) (distance ?from ?to) )
				)
	)
	(:action move-no-tray   
        :parameters  (?from - place ?to - place ?w - waiter)
        :precondition (and (at-waiter ?w ?from) (not (tray-carried ?w)))
        :effect (and  (not(at-waiter ?w ?from)) (at-waiter ?w ?to)
						(increase (time-waiter ?w) (/ (distance ?from ?to) 2))
				)
	)
	(:action pick-up-drink    
        :parameters  (?t - Bar ?w - waiter ?d - drink)
        :precondition 	(and 
						;(is-Bar ?t)
						(not (tray-carried ?w))(at-waiter ?w ?t)
						(order-ready ?d)
						(hand-free ?w)
						(>= (time-waiter ?w)(time-drink-ready ?d))
						)
       :effect (and  (not(order-ready ?d))(not (hand-free ?w))
						(order-carried ?d ?w)
				)
	)
	(:action put-down-drink    
        :parameters  (?t - place ?w - waiter ?d - drink)
        :precondition 	(and (ordered ?d ?t)(not (tray-carried ?w))
						(at-waiter ?w ?t)
						(order-carried ?d ?w)
						(>= (+ 4 (time-drink-ready ?d)) (* (fl-hot ?d)(time-waiter ?w)) )
						)
        :effect (and  (not (order-carried ?d ?w))
						(order-delivered ?d)
						(hand-free ?w)
				        (decrease (fl-customers ?t) 1)
				        (assign (fl-last-delivered ?t) (time-waiter ?w))
				)
	)
	
	(:action pick-2-tray   
        :parameters		(?t - Bar ?w - waiter ?d1 - drink ?d2 - drink)
        :precondition	(and ;(is-Bar ?t)
							(at-waiter ?w ?t)
                            (not (equals ?d1 ?d2))
							(hand-free ?w)(not (tray-taken))(tray-empty)
							(order-ready ?d1)(order-ready ?d2)
							(>= (time-waiter ?w)(time-drink-ready ?d1))
							(>= (time-waiter ?w)(time-drink-ready ?d2))
						)
        :effect (and  (not(order-ready ?d1))(not(order-ready ?d2))
						(not (hand-free ?w))(tray-taken)(not (tray-empty))
						(tray-carried ?w)
						(order-carried ?d1 ?w) (order-carried ?d2 ?w)
						(has2 ?w)
				)
	)
	(:action pick-3-tray   
        :parameters		(?t - Bar ?w - waiter ?d3 - drink)
        :precondition	(and (has2 ?w)
							;(is-Bar ?t)
							(at-waiter ?w ?t)
							(>= (time-waiter ?w)(time-drink-ready ?d3))
							(order-ready ?d3)
						)
        :effect (and  (not(order-ready ?d3))
						(order-carried ?d3 ?w)
						(not (has2 ?w)) (has3 ?w)
				)
	)
	
	(:action put-down-3-drink   
        :parameters		(?t - place ?w - waiter ?d3 - drink)
        :precondition	(and (has3 ?w)(at-waiter ?w ?t)
							(ordered ?d3 ?t)
							(order-carried ?d3 ?w)
							(>= (+ 4 (time-drink-ready ?d3)) (* (fl-hot ?d3)(time-waiter ?w)) )
						)
        :effect (and  (not (order-carried ?d3 ?w))
						(order-delivered ?d3)
						(not (has3 ?w)) (has2 ?w)
						(decrease (fl-customers ?t) 1)
				        (assign (fl-last-delivered ?t) (time-waiter ?w))
				)
	)
	
	(:action put-down-2-drink   
        :parameters		(?t - place ?w - waiter ?d2 - drink)
        :precondition	(and (has2 ?w)(at-waiter ?w ?t)
							(ordered ?d2 ?t)
							(order-carried ?d2 ?w)
							(>= (+ 4 (time-drink-ready ?d2)) (* (fl-hot ?d2)(time-waiter ?w)) )
						)
        :effect (and  (not (order-carried ?d2 ?w))
						(order-delivered ?d2)
						(not (has2 ?w)) (has1 ?w)
						(decrease (fl-customers ?t) 1)
				        (assign (fl-last-delivered ?t) (time-waiter ?w))
				)
	)
	(:action put-down-1-drink   
        :parameters		(?t - place ?w - waiter ?d1 - drink)
        :precondition	(and (has1 ?w)(at-waiter ?w ?t)
							(ordered ?d1 ?t)
							(order-carried ?d1 ?w)
							(>= (+ 4 (time-drink-ready ?d1)) (* (fl-hot ?d1)(time-waiter ?w)) )
						)
        :effect (and  (not (order-carried ?d1 ?w))
						(order-delivered ?d1)
						(not (has1 ?w))
						(tray-empty)
						(decrease (fl-customers ?t) 1)
				        (assign (fl-last-delivered ?t) (time-waiter ?w))
				)
	)
	(:action drop-tray   
        :parameters		(?t - Bar ?w - waiter)
        :precondition	(and (tray-carried ?w)
								;(is-Bar ?t)
								(tray-empty)(at-waiter ?w ?t)
						)
        :effect (and  (not (tray-carried ?w))
						(not (tray-taken))
						(hand-free ?w)
				)
	)
	
	(:action clean-table   
        :parameters		(?t - place ?w - waiter)
        :precondition	(and (empty ?t)(not (clean ?t))
						(at-waiter ?w ?t) (hand-free ?w)
						(not (tray-carried ?w))
						(>= (time-waiter ?w)(fl-time-empty ?t))
						)
        :effect (and  (increase (time-waiter ?w) (* 2 (fl-table-size ?t)))
						(clean ?t)
				)
	)
	(:action set-table-empty   
        :parameters		(?t - place)
        :precondition	(= (fl-customers ?t) 0)
        :effect (and  (assign (fl-time-empty ?t) (+ (fl-last-delivered ?t) 4))
                        (empty ?t)
				)
	)
	
)
       
